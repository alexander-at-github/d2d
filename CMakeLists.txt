cmake_minimum_required (
  VERSION 3.11 FATAL_ERROR
  )
project (
  d2d-superbuild
  VERSION 1.0
  LANGUAGES CXX C
  )

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_CXX_FLAGS_DEBUG "-g -O0")
set(CMAKE_CXX_FLAGS_RELEASE "-O3 -march=native -flto") # -DNDEBUG
# the following two variables will be used in ./external/upstream/
set(M_DEPENDENCIES_DIR ${CMAKE_SOURCE_DIR}/dependencies)
set(STAGED_INSTALL_PREFIX ${M_DEPENDENCIES_DIR}/stage)
include(ExternalProject)
add_subdirectory(external/upstream)
externalproject_add (
  d2d
  DEPENDS vtk-external gmsh-external
  SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/src
  # Eventhough this is a superbuild, we want the final binary in the build
  # directory
  INSTALL_DIR ${CMAKE_BINARY_DIR}
  CMAKE_ARGS
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
    -DCMAKE_CXX_EXTENSIONS=${CMAKE_CXX_EXTENSIONS}
    -DCMAKE_CXX_STANDARD_REQUIRED=${CMAKE_CXX_STANDARD_REQUIRED}
    -DCMAKE_VERBOSE_MAKEFILE=${CMAKE_VERBOSE_MAKEFILE}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
    -DCMAKE_CXX_FLAGS_DEBUG=${CMAKE_CXX_FLAGS_DEBUG}
    -DCMAKE_CXX_FLAGS_RELEASE=${CMAKE_CXX_FLAGS_RELEASE}
    #
    -DGMSH_DIR=${GMSH_DIR}
    -DVTK_DIR=${VTK_DIR}
  CMAKE_CACHE_ARGS
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_PREFIX_PATH:PATH=${CMAKE_PREFIX_PATH}
    -DCMAKE_INCLUDE_PATH:PATH=${CMAKE_INCLUDE_PATH}
    -DCMAKE_LIBRARY_PATH:PATH=${CMAKE_LIBRARY_PATH}
    BUILD_ALWAYS 1
  )
